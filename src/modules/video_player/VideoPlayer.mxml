<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="544" height="356" creationComplete="init()" clipContent="false" implements="modules.video_player.VideoPlayerInterface">
	<mx:Style source="modules/video_player/css/videoPlayer.css" />
	<mx:Style source="modules/video_player/css/SampleCaptioning.css" />
	<mx:Script>
		<![CDATA[	
		
			import mx.core.UIComponent;
			import mx.events.SliderEvent;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import flash.geom.*; 
			import flash.events.FullScreenEvent;
			import flash.display.Stage; 
			import flash.display.StageDisplayState;
			import mx.controls.textClasses.TextRange;
			
			import org.openvideoplayer.events.*;
			import org.openvideoplayer.net.*; 
			import org.openvideoplayer.rss.*;
			import org.openvideoplayer.cc.*;
			
			import com.akamai.net.*;
			import com.akamai.rss.*;
			
			import mx.utils.*;
					
			// Define private variables
			private var _nc:AkamaiConnection;
			private var _ns:AkamaiNetStream;
			private var _bossMetafile:AkamaiBOSSParser;
			private var _sliderDragging:Boolean;
			private var _waitForSeek:Boolean;
			private var _video:Video;
			private var _videoHolder:UIComponent;
			private var _bandwidthMeasured:Boolean;  
			private var _hasEnded:Boolean;
			private var _videoSettings:Object;
			private var _streamLength:Number;
			private var _isPaused:Boolean;
			private var _ccMgr:OvpCCManager;
			private var _captionTimer:Timer;
			private var _ccOn:Boolean;
			private var _ccPositioned:Boolean;
            [Bindable]
            private var video_id:String;
			[BINDABLE]
			public var VIDEO_URL:String = "http://usana.edgeboss.net/flash/usana/"+FlexGlobals.topLevelApplication.current_video.@id+".flv?xmlvers=1";
			private const _CAPTION_URL_:String = "modules/video_player/xml/caption/sample_caption.xml";
	
			/* ================================================== */
			/* = FUNCTION TO CHANGE VIDEO URL AND START PLAYBACK = */
			/* ================================================== */
	 		public function setVideo(id:String):void {
				_ns.pause(); 
	           VIDEO_URL = "http://usana.edgeboss.net/flash/usana/"+id+".flv?xmlvers=1";
	           startPlayback();
	        }
	    
			
			
			// Define functions
			private function init():void {
				
				
				stage.addEventListener(FullScreenEvent.FULL_SCREEN, handleReturnFromFullScreen);
				//
				_bossMetafile = new AkamaiBOSSParser();
				_bossMetafile.addEventListener(OvpEvent.PARSED,bossParsedHandler);
				_bossMetafile.addEventListener(OvpEvent.LOADED,bossLoadHandler);
				_bossMetafile.addEventListener(OvpEvent.ERROR,errorHandler);
				//
				_nc = new AkamaiConnection();
				
				// This example shows all the possible events that you may subscribe to.
				// In a real project, you would only choose a subset of these. 
				_nc.addEventListener(OvpEvent.ERROR,errorHandler);
				_nc.addEventListener(OvpEvent.BANDWIDTH,bandwidthHandler);
				_nc.addEventListener(OvpEvent.STREAM_LENGTH,streamLengthHandler); 
				_nc.addEventListener(NetStatusEvent.NET_STATUS,netStatusHandler);
				
				_bandwidthMeasured = false;
				addVideoToStage();
				//
				_ccOn = true;
				_ccPositioned = false;
				_captionTimer = new Timer(10000);
				_captionTimer.addEventListener(TimerEvent.TIMER, onCaptionTimer);
				
				// This inititates the video playback
				startPlayback();
					
			}	
			// Handles the notification that the BOSS feed was successfully loaded.
			private function bossLoadHandler(e:OvpEvent):void {
				write("BOSS loaded successfully");
			}
			// Handles the notification that the BOSS feed was successfully parsed
			private function bossParsedHandler(e:OvpEvent):void {
				write("BOSS parsed successfully:");
				write("========== Metafile data ==============");
				switch (_bossMetafile.versionOfMetafile) {
					case _bossMetafile.METAFILE_VERSION_I:
						write("  Server name: " + _bossMetafile.serverName);
						write("  Fallback server name: " + _bossMetafile.fallbackServerName);
						write("  App name: " + _bossMetafile.appName);
						write("  Stream name: " + _bossMetafile.streamName);
						write("  Is live: " + _bossMetafile.isLive);
						write("  Buffer time: " + _bossMetafile.bufferTime);
						write("  Connect Auth Params: " + _bossMetafile.connectAuthParams);
						write("  Play Auth Params: " + _bossMetafile.playAuthParams);
						_nc.requestedProtocol = "any";
					break;
					case _bossMetafile.METAFILE_VERSION_II:
						write("  Server name: " + _bossMetafile.serverName);
						write("  App name: " + _bossMetafile.appName);
						write("  Stream name: " + _bossMetafile.streamName);
						write("  Is live: " + _bossMetafile.isLive);
						write("  Buffer time: " + _bossMetafile.bufferTime);
						write("  Connect Auth Params: " + _bossMetafile.connectAuthParams);
						write("  Play Auth Params: " + _bossMetafile.playAuthParams);
						_nc.requestedProtocol = "any";
					break;
					case _bossMetafile.METAFILE_VERSION_IV:
						write("  Server name: " + _bossMetafile.serverName);
						write("  App name: " + _bossMetafile.appName);
						write("  Stream name: " + _bossMetafile.streamName);
						write("  Protocol: " + _bossMetafile.protocol);
						write("  Is live: " + _bossMetafile.isLive);
						write("  Title: " + _bossMetafile.title);
						write("  Source: " + _bossMetafile.source);
						write("  Author: " + _bossMetafile.author);
						write("  Clip begin: " + _bossMetafile.clipBegin);
						write("  Clip end: " + _bossMetafile.clipEnd);
						write("  Duration: " + _bossMetafile.duration);
						write("  Connect Auth Params: " + _bossMetafile.connectAuthParams);
						write("  Play Auth Params: " + _bossMetafile.playAuthParams);
						write("  Secondary Encoder Source: " + _bossMetafile.secondaryEncoderSrc);
						write("  Keywords: " + _bossMetafile.keywords);
						_nc.requestedProtocol = _bossMetafile.protocol.indexOf("rtmpe") != -1 ? "rtmpe,rtmpte":"any";
					break;
				}
				write("======= End of Metafile data ===========");
				// Establish the connection
				trace("requested protocl set to " + _nc.requestedProtocol);
				_nc.connectionAuth = _bossMetafile.connectAuthParams;
				_nc.connect(_bossMetafile.hostName); 
			}
			
			// Once a good connection is found, this handler will be called
			private function connectedHandler():void {
				_ns = new AkamaiNetStream(_nc.netConnection);

				_ns.addEventListener(OvpEvent.COMPLETE,completeHandler); 
				_ns.addEventListener(OvpEvent.PROGRESS,update); 
				_ns.addEventListener(NetStatusEvent.NET_STATUS,streamStatusHandler);
				_ns.addEventListener(OvpEvent.NETSTREAM_PLAYSTATUS,streamPlayStatusHandler);
				_ns.addEventListener(OvpEvent.NETSTREAM_METADATA,metadataHandler);
				_ns.addEventListener(OvpEvent.STREAM_LENGTH,streamLengthHandler);
				
				
				
				// Create the closed captioning object and give it the net stream
				_ccMgr = new OvpCCManager(_ns);
				_ccMgr.addEventListener(OvpEvent.ERROR, errorHandler);		
				_ccMgr.addEventListener(OvpEvent.CAPTION, captionHandler);
				_ccMgr.parse(_CAPTION_URL_); 
				bClosedcaption.styleName = "ccBtnOn";				
				
				_ns.maxBufferLength = 2;
				_ns.liveStreamAuthParams = _bossMetafile.playAuthParams;
				
				_video.attachNetStream(_ns);
				
				
					currentState = "";
					write("Successfully connected to: " + _nc.netConnection.uri);
					write("Port: " + _nc.actualPort);
					write("Protocol: " + _nc.actualProtocol);
					write("Server IP address: " + _nc.serverIPaddress);
					
					// If an ondemand stream, start the asynchronous process of requesting the stream length 
					_nc.requestStreamLength(_bossMetafile.streamName);

					// start the asynchronous process of estimating bandwidth if it hasn't already been esimated
					if (_bandwidthMeasured) {
						playVideo(_bossMetafile.streamName);
					} else {
						write("Measuring bandwidth ... ");
						_nc.detectBandwidth();
					}
				}

			// Handles all error events
			private function errorHandler(e:OvpEvent):void {
				switch(e.data.errorNumber) {
				case OvpError.STREAM_NOT_FOUND:
					Alert.show("Connected to the server at " + _nc.serverIPaddress + " but timed-out trying to locate the live stream " + _bossMetafile.streamName, "UNABLE TO FIND STREAM ", Alert.OK);
					break;
				default:
					Alert.show("Error #" + e.data.errorNumber+": " + e.data.errorDescription, "ERROR", Alert.OK);
					break;
				}
			}
			// Handles the result of the bandwidth estimate
			private function bandwidthHandler(e:OvpEvent):void {
				_bandwidthMeasured  = true;
				write("Bandwidth measured at " + e.data.bandwidth+ " kbps and latency is " + e.data.latency + " ms.");
				// At this stage you would use the bandwidth result in order to choose
				// the appropriate file for the user.
				playVideo(_bossMetafile.streamName);
			}
			// Receives all status events dispatched by the active NetConnection
			private function netStatusHandler(e:NetStatusEvent):void {
				write(e.info.code);
				switch (e.info.code) {
					case "NetConnection.Connect.Rejected":
						write("Rejected by server. Reason is "+e.info.description);
						break;
					case "NetConnection.Connect.Success":
						connectedHandler();
						break;
				}
			}
			// Receives all status events dispatched by the active NetStream
			private function streamStatusHandler(e:NetStatusEvent):void {
				write(e.info.code);	
				if (e.info.code == "NetStream.Buffer.Full") {
					// _waitForSeek is used to stop the scrubber from updating
					// while the stream transtions after a seek
					_waitForSeek = false;
				}
			}
			// Receives all onPlayStatus events dispatched by the active NetStream
			private function streamPlayStatusHandler(e:OvpEvent):void {
				write(e.data.code);
			}
			// Receives all onMetadata events dispatched by the active NetStream
			private function metadataHandler(e:OvpEvent):void {
				positionCaption();
				
				write("========== Metadata found in stream  ===========");
				for (var propName:String in e.data) {
					write("  "+propName+" = "+e.data[propName]);
				}
				write("========== End of Stream Metadata  ===========");
				
				//Alert.show(_video.width.toString() + "  :  " + _video.height.toString());
				
				// !!!!  For some reason the videos are showing as 320 x 240 in the metadata but they are really 544 x 306
				
				// Adjust the video dimensions on the stage if they do not match the metadata
				if ((Number(e.data["width"]) != _video.width)  || (Number(e.data["height"]) != _video.height)) {
					scaleVideo(Number(e.data["width"]),Number(e.data["height"]));
				}
			}
			// Scales the video to fit into the 544x306 window while preserving aspect ratio.
			private function scaleVideo(w:Number,h:Number):void {
				  if (w/h <= 4/3) {
					_video.width = 306*w/h;
					_video.height = 306;
				} else {
					_video.width = 544;
					_video.height = 544*h/w;
				}
				_video.x = (_videoHolder.width-_video.width)/2;
				_video.y = (_videoHolder.height-_video.height)/2;
				_video.visible = true;
				
			}
			
						
			// Handles the stream length response after a request to requestStreamLength
			private function streamLengthHandler(e:OvpEvent):void {
				write("Stream length is " + e.data.streamLength);
				slider.maximum = e.data.streamLength;
				slider.enabled = true;
				bPlayPause.enabled = true;
				bFullscreen.enabled = true;
				_streamLength = e.data.streamLength;
			}
			
			// Receives information that stream playback is complete. This notification
			// should not be used when playing back progressive content as the Flash client
			// does not dispatch the NetStream.onPlayStatus event on which this notification is based.
			private function completeHandler(e:OvpEvent):void {
				write("Stream is complete");
				_hasEnded = true;
				bPlayPause.styleName = "vpPlayBtn";
			}
			// Attaches the video to the stage
			private function addVideoToStage():void {
				_videoHolder= new UIComponent();
				_video = new Video(544,306);
				_video.smoothing = true;
				_video.visible = false;
				_video.x = (_videoHolder.width-_video.width)/2;
				_video.y = (_videoHolder.height-_video.height)/2;
				_videoHolder.addChild(_video);
        		videoWindow.addChild(_videoHolder);
   			}
   			// Plays the stream 
   			private function playVideo(name:String):void {
   				_ns.play(name);
   				bPlayPause.styleName = "vpPauseBtn";  				
   			}
   			// Updates the time display and slider
   			private function update(e:OvpEvent):void {
   				timeDisplay.text =  _ns.timeAsTimeCode + "|"+ _nc.streamLengthAsTimeCode(_streamLength);

   					if (!_sliderDragging && !_waitForSeek) {
   						slider.value = _ns.time;
   					
   				}
   			}
   			// Seeks the stream after the slider is dropped
   			private function doSeek():void {
   				hideCaption();
   				write("calling seek to " + slider.value);
   				if (_hasEnded) {
   					_hasEnded = false;
   					_ns.play(VIDEO_URL);
   					bPlayPause.styleName = "vpPauseBtn";
   				} 
   				_ns.seek(slider.value);
   			}
   			// Toggles the dragging state
   			private function toggleDragging(state:Boolean):void {
   				_sliderDragging = state;
   				if (!state) {
   					_waitForSeek = true;
   					doSeek();
   				}
   			}
   			
   			// Commences connection to a new link
			private function startPlayback():void {
				//output.text = "";
				bPlayPause.enabled = false;
				bFullscreen.enabled = false;
				_hasEnded = false;
				// Clean up from previous session, if it exists
				if (_nc.netConnection is NetConnection) {
					_ns.useFastStartBuffer = false;
					_nc.close();
				}

					_bossMetafile.load(VIDEO_URL)
					_isPaused = false;
				
			}
			
			
			
			
			
   			// Handles play and pause
   			private function doPlayPause():void {
   				switch (_isPaused){
   					case false:
   						bPlayPause.styleName = "vpPlayBtn";
   						_ns.pause();
   						_isPaused=true;
   					break;
   					case true:
   						bPlayPause.styleName = "vpPauseBtn";
   						if (_hasEnded) {
   							_hasEnded = false;
   							_ns.play(VIDEO_URL);
   							_isPaused=false;
   						} else {
   							_ns.resume();
   							_isPaused=false;
   						}
   					break;
   				}
   			}
   			// Formats the slider dataTip
			private function showVolume(val:String):String {
				return ("Volume: "+Math.round(Number(val)*100)+"%");
			}
			// Converts time to timecode
			private function showScrubTime(val:String):String {
	   			var sec:Number = Number(val);
				var h:Number = Math.floor(sec/3600);
				var m:Number = Math.floor((sec%3600)/60);
				var s:Number = Math.floor((sec%3600)%60);
				return (h == 0 ? "":(h<10 ? "0"+h.toString()+":" : h.toString()+":"))+(m<10 ? "0"+m.toString() : m.toString())+":"+(s<10 ? "0"+s.toString() : s.toString());
			}
			// Changes the stream volume
			private function changeVolume(event:SliderEvent):void {
				//if (_ns is AkamaiConnection) 
					_ns.volume = event.value;
			}
			// Writes trace statements to the output display
			private function write(msg:String):void {
				//output.text += msg + "\n";
				//output.verticalScrollPosition = output.maxVerticalScrollPosition+1;
			}
			// Switches to full screen mode
			private function switchToFullScreen():void {
				    // when going out of full screen mode 
				    // we use these values
				   
				    _videoSettings = new Object();
				    _videoSettings.savedWidth = _videoHolder.width;
				    _videoSettings.savedHeight = _videoHolder.height;
				    _videoSettings.x = _videoHolder.x;
				    _videoSettings.y = _videoHolder.y;
				  
				     
				    stage["fullScreenSourceRect"] = new Rectangle(0,0, _video.videoWidth ,_video.videoHeight);	 
				    stage["displayState"] = StageDisplayState.FULL_SCREEN;
				    
				    videoWindow.removeChild(_videoHolder);
				    addChild(_videoHolder);
				   	_video.x = _videoHolder.x = 0;
				   	_video.y = _videoHolder.y = 0;
				    _video.width =  _videoHolder.width = _video.videoWidth;
				    _video.height =  _videoHolder.height = _video.videoHeight;
				    _video.width =  _videoHolder.width = _video.videoWidth;
				    _video.height =  _videoHolder.height = _video.videoHeight;
				    _video.smoothing = true;
	

				   
			}
			// Handles the return from fullscreen
			private function handleReturnFromFullScreen(e:FullScreenEvent):void {
				if (!e.fullScreen) {
					removeChild(_videoHolder);
					videoWindow.addChild(_videoHolder);
				    _video.smoothing = true;
				    _videoHolder.width = _videoSettings.savedWidth;
				    _videoHolder.height = _videoSettings.savedHeight;
				    _videoHolder.x = _videoSettings.x
				   	_videoHolder.y = _videoSettings.y
				    scaleVideo(_video.videoWidth,_video.videoHeight);
				}
			}
			
			// Handles the displaying of the caption
		private function showCaption(ccObj:Caption):void {
			captionLabel.htmlText = ccObj.text;
			
			// IMPORTANT!  This line causes the LayoutManager to run immediately, allowing us to change the style below.
			captionLabel.validateNow();	
		
			// Formatting within the caption string			
			for (var i:uint = 0; i < ccObj.captionFormatCount(); i++) {
				var ccFormatObj:CaptionFormat = ccObj.getCaptionFormatAt(i);
				var txtRange:TextRange = new TextRange(captionLabel, false, ccFormatObj.startIndex, ccFormatObj.endIndex);
				var styleObj:Style = ccFormatObj.styleObj;
				
				if (styleObj) {
					if (styleObj.backgroundColor != "") {
						// FYI: setStyle is one of the most expensive calls, performance-wise, in the Flex SDK!
						captionLabel.setStyle("backgroundColor", styleObj.backgroundColor);
					}
					
					callLater(changeTextRange, [txtRange, styleObj]);
					
					if (captionLabel.wordWrap != styleObj.wrapOption) {
						captionLabel.wordWrap = styleObj.wrapOption;
					}
				}
			}
				
			captionLabel.visible = true;
			
			if (ccObj.endTime > 0) {
				_captionTimer.delay = (ccObj.endTime - ccObj.startTime)*1000;
				_captionTimer.start();
			}
		}
		
		private function changeTextRange(txtRange:TextRange, styleObj:Style):void {
			if (styleObj.color != "") {
				txtRange.color = styleObj.color;
			}
			if (styleObj.fontFamily != "") {
				txtRange.fontFamily = styleObj.fontFamily;
			}
			if (styleObj.fontSize > 0) {
				txtRange.fontSize = styleObj.fontSize;
			}
			if (styleObj.fontStyle != "") {
				txtRange.fontStyle = styleObj.fontStyle;
			}
			if (styleObj.fontWeight != "") {
				txtRange.fontWeight = styleObj.fontWeight;
			}
			if (styleObj.textAlign != "") {
				txtRange.textAlign = styleObj.textAlign;
			}			
		}
		
		// Handles the hiding of the caption
		private function hideCaption():void {
			_captionTimer.stop();
			captionLabel.visible = false;
		}
		
		private function onCaptionTimer(e:TimerEvent):void {
			trace("******* onCaptionTimer *********");
			this.hideCaption();
		}
		
		// Receives caption events dispatched by OvpCCManager class
		private function captionHandler(e:OvpEvent):void {
			if (e && e.data && (e.data is Caption)) {
				hideCaption();
				showCaption(Caption(e.data));				
			}
		}
		
		private function positionCaption():void {
			// only need to do this once
			if (!_ccPositioned) {
				_ccPositioned = true;
				var pt:Point = videoContainer.contentToGlobal(new Point(videoContainer.x, videoContainer.y));
				pt = captionLabel.globalToContent(pt);
				captionLabel.x = pt.x;
				captionLabel.y = pt.y + videoWindow.height - captionLabel.height;	
					
			}
		}
		
		private function onClickCC(e:Event):void {
			_ccOn = !_ccOn;
			captionLabel.visible = _ccOn;
			_ccMgr.enableCuePoints(_ccOn);
			var color:String = _ccOn ? "#00cc00" : "#cc0000";
			//ccBox.setStyle("borderColor", color);
			if (!_ccOn) {
				captionLabel.htmlText = "";
				bClosedcaption.styleName = "ccBtn";
			} else {
				bClosedcaption.styleName = "ccBtnOn";
			}
		}
		]]>
	</mx:Script>
	


	    <mx:HBox>
	    
	    <!-- BEGIN VIDEO PLAYER-->
	    
		<mx:Canvas id="videoContainer" width="544" height="359" backgroundAlpha="0.09" backgroundColor="#000000" horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:VBox x="0" y="0" height="359" verticalGap="0" width="544" horizontalScrollPolicy="off" verticalScrollPolicy="off" borderStyle="none" horizontalAlign="center" verticalAlign="middle">
			  <mx:HBox width="544" height="306" id="videoWindow" borderStyle="none" verticalAlign="middle" horizontalAlign="center" backgroundColor="#000000"/>   	
			  	<mx:HBox width="{videoWindow.width}" height="50" verticalAlign="middle" styleName="controlBarBackground">
			  	     <mx:Button id="bPlayPause" click="doPlayPause()" styleName="vpPlayBtn" buttonMode="true" useHandCursor="true"/>
			  	     <mx:Spacer width="5" />
			  	     <mx:HSlider 
			  	     	allowTrackClick="true"
			  	     	buttonMode="true" 
			  	     	useHandCursor="true"
			  	     	showTrackHighlight="true" 
			  	     	sliderThumbClass="modules.video_player.vpSliderThumb" 
			  	     	trackSkin="modules.video_player.vpSliderTrack"
			  	     	trackHighlightSkin="modules.video_player.vpSliderHighlight"
			  	     	styleName="vpSlider" width="265" id="slider" 
			  	     	enabled="false" 
			  	     	dataTipFormatFunction="showScrubTime" 
			  	     	thumbPress="toggleDragging(true)" 
			  	     	thumbRelease="toggleDragging(false)" />
			  	     <mx:Spacer width="1" />
			  	     <mx:Text id="timeDisplay" styleName="timer"  text="00:00/00:00"/>
			  	     <mx:Spacer width="25" />
			  	     <mx:Button id="bFullscreen" click="switchToFullScreen()" enabled="false" styleName="fsBtn" buttonMode="true" useHandCursor="true"/>
			  	     <mx:Button id="bClosedcaption" styleName="ccBtn" click="onClickCC(event)" buttonMode="true" useHandCursor="true"/>
			  	     <mx:Spacer width="5" />
			  	     <mx:HSlider id="volumeSlider"
			  	     	buttonMode="true" 
			  	     	useHandCursor="true" 
			  	     	width="56" value=".75" 
			  	     	showTrackHighlight="true" 
			  	     	trackSkin="modules.video_player.vpSliderTrack"
			  	     	sliderThumbClass="modules.video_player.vpSliderThumb" 
			  	     	trackHighlightSkin="modules.video_player.vpSliderHighlight"
			  	     	styleName="vpSlider" 
			  	     	dataTipFormatFunction="showVolume" 
			  	     	minimum="0" maximum="1" 
			  	     	change="changeVolume(event)" 
			  	     	allowTrackClick="true" 
			  	     	liveDragging="true"/>
			  	</mx:HBox>
			</mx:VBox>
		</mx:Canvas>
		
		<!-- END VIDEO PLAYER-->
		
		</mx:HBox>
		
		<!-- SHOW CC -->
				
		<mx:TextArea wordWrap="true" id="captionLabel" styleName="captionStyle" color="0xFFFFFF"
		width="544" borderStyle="none" horizontalScrollPolicy="off" verticalScrollPolicy="off" visible="false"/>

</mx:Module>
